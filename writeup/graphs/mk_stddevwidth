#!/usr/bin/env gnuplot
set key top left
set border 3
set tics nomirror out
set linetype 1 lc rgb "#80D480" lw 5
set linetype 2 lc rgb "#008200" lw 5
set linetype 3 lc rgb "#2B2BB2" lw 5
set linetype 4 lc rgb "#B22B2B" lw 5
set term pdf enhanced color solid font "URWPalladioL" fontscale 0.5

maximum(x,y)=(x>y)?x:y
diff(x,y)=abs(x-y)
maxErr(x,y,z)=maximum(diff(x,y),diff(x,z))
f(x)=sqrt(x/16+(x*x-3*x+2)/48)
g(x)=sqrt((x**2-4)/48)
h(x)=x/sqrt(48)

set xlabel "Line Width"
set ylabel "Distance"
set output "stddevwidth.pdf"

plot [1:5] "dat/test.dat" u 1:(f($1)):(g($1)) w filledcurves title "Error Range in Approximation",\
h(x) title "Approximate StdDev",\
"dat/test.dat" u 1:3 w l title "Actual Standard Deviation",\
"dat/test.dat" u 1:(abs(h($1)-$3)) w l title "Maximum Error in approximation"
